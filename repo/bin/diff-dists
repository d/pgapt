#!/usr/bin/perl

# Diff two Sources files, comparing version numbers for each package found in the second file.
# Usage example: ./diff-dists <debian-sid-main-Sources> <pgapt-sid-pgdg-testing-main-Sources>

use strict;
use warnings;
use Dpkg::Version;

my $fulldiff = 0;
if (@ARGV and $ARGV[0] eq '-f') {
	shift;
	$fulldiff = 1;
}

die "Syntax: $0 [-f] <source1> <source2>" unless (@ARGV == 2);
my ($source1, $source2) = @ARGV;
my (%pkg1, %pkg2);

$/ = ''; # slurp paragraphs

open S2, $source2 or die "$source2: $!";
while (<S2>) {
	my ($pkg) = /^Package: (.+)/m or die "$source2: paragraph without Package: $_";
	my ($ver) = /^Version: (.+)/m or die "$source2: paragraph without Version: $_";
	my $v = Dpkg::Version->new($ver);
	$pkg2{$pkg} = $v if (not exists $pkg2{$pkg} or $v > $pkg2{$pkg});
}
close S2;

open S1, $source1 or die "$source1: $!";
while (<S1>) {
	my ($pkg) = /^Package: (.+)/m or die "$source1: paragraph without Package: $_";
	my ($ver) = /^Version: (.+)/m or die "$source1: paragraph without Version: $_";
	my $v = Dpkg::Version->new($ver);
	$pkg1{$pkg} = $v if (not exists $pkg1{$pkg} or $v > $pkg1{$pkg});
}
close S1;

foreach my $pkg (sort keys %pkg1) {
	my $ver1 = my $ver1mangled = $pkg1{$pkg};
	$ver1mangled =~ s/.pgdg(?:[\d.]+)?\+\d$//; # remove .pgdg80+1
	if (not exists $pkg2{$pkg}) {
		print "$pkg $ver1 <missing>\n" if ($fulldiff);
		next;
	}

	my $ver2 = my $ver2mangled = $pkg2{$pkg};
	$ver2mangled =~ s/.pgdg(?:[\d.]+)?\+\d$//; # remove .pgdg80+1
	if ($ver1 eq $ver2 or $ver1 eq $ver2mangled or $ver1mangled eq $ver2mangled) {
		#print "OK: $pkg $ver1 $ver2\n";
		delete $pkg2{$pkg};
		next;
	}

	print "$pkg $ver1 $ver2\n";
	delete $pkg2{$pkg};
}

foreach my $pkg (sort keys %pkg2) {
	print "$pkg <missing> $pkg2{$pkg}\n";
}
