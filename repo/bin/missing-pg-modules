#!/usr/bin/perl

use strict;
use warnings;

my $PG_OLDEST_VERSION; # oldest version to check
my $PG_DEVEL_VERSION;  # ignore devel

# read pgapt config
foreach my $dir (qw(. .. /srv/apt/apt.postgresql.org/)) {
  next unless (-f "$dir/pgapt.conf");
  open F, "$dir/pgapt.conf";
  while (<F>) {
    $PG_OLDEST_VERSION = $1 if /^PG_OLDEST_VERSION=(.+)/;
    $PG_DEVEL_VERSION = $1 if /^PG_DEVEL_VERSION=(.+)/;
  }
  close F;
  last;
}

my $packages = shift || 'sid-pgdg-testing';
$packages = "dists/$packages/main/binary-amd64/Packages" unless ($packages =~ m!/!);

my %pkgs; # known binary packages
my %templates; # binary package templates, e.g. postgresql-*-debversion
my %pgversions; # all existing PostgreSQL versions

$/ = ''; # slurp paragraphs

open P, $packages or die "$packages: $!";
while (<P>) {
	my ($pkg) = /^Package: (.+)/m or die "$packages: paragraph without Package: $_";
	if ($pkg =~ /-([89]\.\d|1\d)\b/) {
		my $version = $1;
		next if ($version < $PG_OLDEST_VERSION);
		next if ($PG_DEVEL_VERSION and $version eq $PG_DEVEL_VERSION);
		$pkgs{$pkg} = 1;
		$pkg =~ s/-([89]\.\d|1\d)\b/-*/;
		$templates{$pkg} = 1;
		$pgversions{$version} = 1;
	}
}
close P;

foreach my $template (sort keys %templates) {
	my @missing;
	foreach my $version (sort { $a <=> $b } keys %pgversions) {
		my $pkg = $template;
		$pkg =~ s/-\*/-$version/;
		next if ($pkgs{$pkg});
		push @missing, $version;
	}
	print "$template: @missing\n" if (@missing);
}
